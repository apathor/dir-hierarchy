#!/usr/bin/env perl

use v5.20;
use strict;
use warnings;

use Regexp::Common qw/net URI/;
use lib './lib/';
use Dir::Hierarchy;

$|++;

my $dir = shift;
die "Directory argument required." unless($dir);
die "Not a directory: $dir" unless(-d $dir);

my $ipv4 = Dir::Hierarchy->new(
  "$dir/ipv4",
  sub { local $_ = lc shift; split /\./ if(/^$RE{net}{IPv4}$/); },
  sub { local $_ = join '.', splice @_, 0, 4; $_ if(/^($RE{net}{IPv4}$)/); },
 );

my $fqdn = Dir::Hierarchy->new(
  "$dir/fqdn",
  sub { local $_ = lc shift; reverse split /\./ if(/^$RE{net}{domain}\.?$/); },
  sub { local $_ = join '.', reverse @_; $_ if(/^($RE{net}{domain}\.?$)/); },
 );

my $cmd = shift;

if($cmd) {
  say for $ipv4->all;
  say for $fqdn->all;
} else {
  # load from stdin
  while(<>) {
    # ipv4
    for (/\b($RE{net}{IPv4})\b/g) {
      $ipv4->add($_);
      printf "%s %s\n", $_, $ipv4->get($_);
    }
    # fqdn
    for(/\b($RE{net}{domain})\b/g) {
      next unless((scalar split /\./) > 1);
      $fqdn->add($_);
      printf "%s %s\n", $_, $fqdn->get($_);
    }
  }
}
